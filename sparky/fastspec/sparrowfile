use Sparky::JobApi;
use HTTP::Tiny;

class Pipeline does Sparky::JobApi::Role {

  has Str $.worker = %*ENV<WORKER> || tags()<worker>;
  has Str $.notify-api = %*ENV<NOTIFY_API> || "";
  has Str $.notify-project = %*ENV<NOTIFY_PROJECT> || "";
  has Str $.notify-job = %*ENV<NOTIFY_JOB> || "";
  has Str $.spec-chunks = %*ENV<SPEC_CHUNKS> || tags()<spec_chunks> || "";
  has Str $.build-rakudo = tags()<build_rakudo> || "yes";

  method stage-test {

    directory "rakudo";

    git-scm "https://github.com/rakudo/rakudo.git", %(
      to => "rakudo"
    );

    bash q:to /HERE/, %( description => "install rakudo", cwd => "{$*CWD}/rakudo") if $.build-rakudo eq "yes";
      set -x
      export MAKEFLAGS=-j6 \
      perl Configure.pl \
      --gen-moar --gen-nqp --backends=moar \
      && make install
    HERE

    bash "git clone https://github.com/Raku/roast.git t/spec", %(
      cwd => "{$*CWD}/rakudo",
      description => "clone roast spec"
    );

    my @lines = "rakudo/t/spec/spectest.data".IO.lines;

    my $lines-cnt = @lines.elems;

    my $chunk = ($lines-cnt / $.spec-chunks.Int ).Int;

    say "spectest.data lines cnt: {$lines-cnt} | chunk size: {$chunk}";

    my @t; 

    my $from = $chunk*($.worker.Int - 1);

    my $to = $.worker.Int == $.spec-chunks.Int ?? ( @lines.elems - 1 ) !! $chunk*($.worker.Int );

    say "chunk, process lines [$from ..$to]";

    for $from .. $to   -> $i {
        @t.push: @lines[$i];
    }

    "rakudo/t/spec/spectest.chunk.data".IO.spurt(@t.join("\n"));

    bash q:to /HERE/, %( cwd => "{$*CWD}/rakudo" , description => "spectest");
      export TEST_JOBS=4
      perl -Itools/lib \
      -I3rdparty/nqp-configure/lib \
      t/harness5 --moar --fudge --keep-exit-code --tests-from-file=t/spec/spectest.chunk.data
    HERE

  }


  method stage-main {

      my $j = self.new-job;

      $j.queue({
        description => "test",
        tags => %(
          stage => "test"
        ),
      });


      my $s = self.wait-job($j);

      my $nj = self.new-job: 
        :api($.notify-api), 
        :project($.notify-project), 
        :job-id($.notify-job);


      my %j = $j.info;

      my $r = HTTP::Tiny.get: "http://127.0.0.1:4000/report/raw/{%j<project>}/{%j<job-id>}";

      my $log = $r<content> ?? $r<content>.decode !! '';

      if $s<OK> {
        $nj.put-stash({ status => "OK", log => $log  });
      } else {
        $nj.put-stash({ status => "FAIL", log => $log  });
      }

      $nj.queue({
        description => "testspec 0{$.worker} report",
        tags => %(
          stage => "notify",
          worker => $.worker
        ),
      });
      
  }

  method stage-notify {

    my $nj = self.new-job: :mine(True);

    my $report = $nj.get-stash();

    say "=========================";

    say "status: ", $report<status>;

    say "log: ", $report<log>;

    bash "az container delete -g sparky --name spk-0{$.worker} -y -o table || echo", %(
      description => "delete container";
    );

  }

}

Pipeline.new.run;  
